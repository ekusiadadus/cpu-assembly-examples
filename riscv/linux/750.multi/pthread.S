/*
 * pthread (generating a thread) example for RISC-V(RV64)/Linux
 *
 *   * Threads
 *      * main:  a main thread
 *      * child: a child thread (generated by pthread_create)
 *
 *   * see:
 *     * man pthread_create
 *     * man pthread_join
 */

        .globl  main

main:
        /* puts for trace-log */
        la      a0, fmtMain_start
        jal     puts


        /* create a thread with pthread_create() */
        la      a0, tid                 /* pthread_t: &tid       */
        li      a1, 0                   /* pthread_attr_t: NULL  */
        la      a2, child               /* start_routine: &child */
        li      a3, 0                   /* arg: NULL             */
        jal     pthread_create


        /* wait the thread with pthread_join() */
        la      t0, tid
        ld      a0, (t0)                /* pthread_t: tid */
        li      a1, 0                   /* retval: NULL   */
        jal     pthread_join


        /* puts for trace-log */
        la      a0, fmtMain_finish
        jal     puts

        /* exit from main */
        li      a0, 0                   /* status: EXIT_SUCCESS */
        jal     exit




        /* a child thread */
child:
        addi    sp,sp,-8
        sd      ra,(sp)

        /* puts for trace-log */
        la      a0, fmtChild
        jal     puts

        /* finish this thead */
        li      a0, 0                   /* return value (not used) */
        ld      ra,(sp)
        addi    sp,sp,8
        ret



        /* read-only data */
        .section .rodata
fmtMain_start:
        .string "main(): start"
fmtMain_finish:
        .string "main(): finish"
fmtChild:
        .string "child(): start"



        /* read-write data */
        .data
        .balign 8
tid:
        .quad   0
